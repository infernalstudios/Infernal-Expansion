buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven/' }
        maven { url = 'https://plugins.gradle.org/m2/' }

        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle',           name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered',                   name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur',    version: '1.2.1'
        classpath group: 'gradle.plugin.com.matthewprenger',    name: 'CurseGradle', version: '1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.matthewprenger.cursegradle'

// This initializes the config. Please try to modify the variables in the config file, if needed, add more if you see a reason to.
ext.config = parseConfig(file('build.properties'))

version = config.VERSION
group = "${config.GROUP}.${config.ARTIFACT}"
archivesBaseName = "${config.ARCHIVES_BASE_NAME}-${config.MINECRAFT_VERSION}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: config.MAPPINGS_CHANNEL, version: config.MAPPINGS_VERSION

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg '-mixin.config=infernal-expansion.mixins.json'
            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                infernalexp {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg '-mixin.config=infernal-expansion.mixins.json'
            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                infernalexp {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'infernalexp', '--all', '--output', file('src/generated/resources/')
            mods {
                infernalexp {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, 'infernal-expansion.refmap.json'
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven/' }

    jcenter()
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.MINECRAFT_VERSION}-${config.FORGE_VERSION}"
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

jar {
    manifest {
        attributes([
                'Specification-Title'   : config.TITLE,
                'Specification-Vendor'  : config.AUTHOR,
                'Specification-Version' : '1',
                'Implementation-Title'  : config.TITLE,
                'Implementation-Version': config.VERSION,
                'Implementation-Vendor' : config.AUTHOR,
                'MixinConfigs'          : 'infernal-expansion.mixins.json',
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

tasks.build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = config.GROUP
            artifactId = config.ARTIFACT
            artifact jar
            artifact sourcesJar
            version "${config.VERSION}-${config.MINECRAFT_VERSION}"

            pom {
                name = 'Infernal Expansion'
                url = 'https://infernalstudios.org/infernalexp'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/infernalexp/Infernal-Expansion.git'
                    url = 'https://github.com/infernalexp/Infernal-Expansion'
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://maven.infernalstudios.org/releases'
            
            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_TOKEN')
            }
            
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

// Modrinth publishing
import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload) {
    onlyIf {
        System.getenv('MODRINTH')
    }

    token = System.getenv('MODRINTH')
    projectId = 'ZrpxHZN4'
    versionNumber = project.version
    uploadFile = jar
    addFile(sourcesJar)
    addGameVersion(config.MINECRAFT_VERSION)
    addLoader('forge')
}

// Curseforge publishing
curseforge {
  apiKey = System.getenv('CURSEFORGE') ?: 'If you\'re seeing this, you haven\'t set your "CURSEFORGE" environment variable'
  
  project {
    id = '395078'
    changelog = 'No changelog specified.'
    releaseType = 'release'
    addGameVersion config.MINECRAFT_VERSION
    mainArtifact (jar) { 
      displayName = "Infernal Expansion Release ${project.version}"
    }
    addArtifact sourcesJar
  }
}


// Config parse function
def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}
